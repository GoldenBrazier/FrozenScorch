# Copyright 2021 Nikita Melekhin. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

template("executable_macos") {
  assert(defined(invoker.sources),
         "Need sources in $target_name to build the App")

  ld_flags = []
  if (defined(invoker.ldflags)) {
    ld_flags = invoker.ldflags
  }
  if (defined(invoker.frameworks)) {
    foreach(i, invoker.frameworks) {
      ld_flags += [ "-framework", i ]
    }
  }

  executable(target_name) {
    sources = invoker.sources
    ldflags = ld_flags
    forward_variables_from(invoker,
                           [
                             "output_name",
                             "libs",
                             "deps",
                             "include_dirs",
                             "configs",
                             "cflags",
                             "cflags_c",
                             "cflags_cc",
                             "cflags_objc",
                             "cflags_objcc",
                             "asmflags",
                             "public_deps",
                           ])
  }
}
